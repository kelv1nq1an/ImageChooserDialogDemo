apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 你的library版本号
version = "1.0.1"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    // 资源前缀
    resourcePrefix "kelvinqian_"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 增加lint选项，不加的话下面上传到jCenter有可能会报错
    lintOptions {
        abortOnError false
    }
}

buildscript {
    repositories {
        jcenter()
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

// 你的library的地址
def siteUrl = 'https://github.com/kelv1nq1an/ImageChooserDialogDemo'
def gitUrl = 'https://github.com/kelv1nq1an/ImageChooserDialogDemo.git'
def issueUrl = 'https://github.com/kelv1nq1an/ImageChooserDialogDemo/issues'

// 这个蛮重要的，你上传到jCenter后一些文件存放的路径，如果不写或者写错了的话
// 可以不会产生pom文件，后面上传到jCenter后会报错，这个问题我搞了一下午！
group = "me.fattycat.kun"
install {
    repositories.mavenInstaller {
        // POM.xml文件内容
        pom {
            project {
                packaging 'aar'
                // 添加描述，不重要
                name 'image chooser dialog'
                url siteUrl
                // 协议
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kelvinqian' //your user ID
                        name 'Fattycat.me' //your name
                        email 'qxm329232741@gmail.com' //your email
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


task javadoc(type: Javadoc) {

    //增加编码，这行很重要，如果是windows系统，同时如果在代码中有中文的话，不加这行，上传会报错
    options.encoding="utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        //发布到Bintray的哪个仓库
        repo = "maven"
        //发布到Bintray上的名字
        name = "imagechooserdialog"
        //项目描述
        desc = "Another image chooser dialog"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        licenses = ["Apache-2.0"]
        //标签
        labels = ['android']
        publish = true
        publicDownloadNumbers = true
    }
}